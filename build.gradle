plugins {
    id 'java-library'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id 'io.codearte.nexus-staging' version '0.21.2'
    id("org.jetbrains.kotlin.jvm") version "${kotlin_version}"
    id("org.jetbrains.kotlin.kapt") version "${kotlin_version}"
}

ext.apiVersion = "0.4-SNAPSHOT"

allprojects {
    group = "org.anvilpowered"
    version = "0.4.0-SNAPSHOT"
    apply plugin: 'maven-publish'
    apply plugin: 'signing'
    apply plugin: 'org.jetbrains.kotlin.jvm'

    repositories {
        mavenCentral()
    }

    compileKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }

    compileTestKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }
}

String _username = project.hasProperty("username") ? username : ""
String _password = project.hasProperty("password") ? password : ""
String _keyId = project.hasProperty("keyId") ? keyId : ""
String _ringFile = project.hasProperty("ringFile") ? ringFile : ""

subprojects {
    apply plugin: 'com.github.johnrengelman.shadow'
    sourceCompatibility = 1.8
    repositories {
        mavenCentral()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
        maven { url 'https://repo.spongepowered.org/repository/maven-public/' }
        maven { url 'https://packages.jetbrains.team/maven/p/xodus/xodus-daily' }
        maven { url 'https://dl.bintray.com/kotlin/kotlinx' }
    }
    if (project.hasProperty("buildNumber") && version.contains("-SNAPSHOT")) {
        version = version.replace("-SNAPSHOT", "-RC${buildNumber}")
    }
    processResources {
        expand project.properties
    }
    nexusStaging {
        packageGroup = 'org.anvilpowered'
        username = _username
        password = _password
    }

    if (project.hasProperty("sign")) {
        ext.'signing.keyId' = _keyId
        ext.'signing.password' = _password
        ext.'signing.secretKeyRingFile' = _ringFile
        signing {
            sign publishing.publications
            sign configurations.archives
        }
    }
}

project(':anvil-api') {
    apply plugin: 'java-library'
    java {
        withJavadocJar()
        withSourcesJar()
    }
    afterEvaluate {
        publishing {
            repositories {
                maven {
                    credentials {
                        username _username
                        password _password
                    }
                    def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                    def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
                    url version.endsWith("SNAPSHOT") ? snapshotsRepoUrl : releasesRepoUrl
                }
            }
            publications {
                mavenJava(MavenPublication) {
                    from components.java

                    pom {
                        name = 'Anvil'
                        description = 'A cross-platform database API / ORM / entity framework with useful services for minecraft plugins'
                        url = 'https://github.com/AnvilPowered/Anvil'

                        scm {
                            url = 'https://github.com/AnvilPowered/Anvil'
                            connection = 'scm:git:https://github.com/AnvilPowered/Anvil.git'
                            developerConnection = 'scm:git:https://github.com/AnvilPowered/Anvil.git'
                        }

                        licenses {
                            license {
                                name = 'GNU LESSER GENERAL PUBLIC LICENSE Version 3'
                                url = 'https://www.gnu.org/licenses/lgpl-3.0.html'
                                distribution = 'repo'
                            }
                        }

                        developers {
                            developer {
                                id = "alexstaeding"
                                name = "Alexander Staeding"
                                organization = "AnvilPowered"
                            }
                            developer {
                                id = "STG-Allen"
                                name = "Jacob Allen"
                                organization = "AnvilPowered"
                            }
                        }
                    }
                }
            }
        }
    }
}
